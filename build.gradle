plugins {
    id 'java-library'
    id "com.vanniktech.maven.publish" version "0.29.0"
    id 'signing'
}

group = project.hasProperty('projectGroup') ? project.property('projectGroup') : 'default.group'
version = project.hasProperty('projectVersion') ? project.property('projectVersion') : 'version'
def projectCoordinate = project.hasProperty('projectCoordinate') ? project.property('projectCoordinate') : 'default_coord'
String projectName = project.hasProperty('projectName') ? project.property('projectName') : 'default_name'
import com.vanniktech.maven.publish.SonatypeHost

mavenPublishing {
    if (version.endsWith("SNAPSHOT")) {
        publishToMavenCentral(
                new SonatypeHost('https://oss.sonatype.org/content/repositories/snapshots/', false)
        )
    } else {
        publishToMavenCentral(SonatypeHost.CENTRAL_PORTAL, true)
    }
    signAllPublications()
    coordinates(group, projectCoordinate, version)
    pom {
        name.set(projectName)
        description.set("Single State Lock-free Reactive Framework.")
        url.set("https://github.com/Skylarkarms/".concat(projectName))
        licenses {
            license {
                name.set("GNU General Public License v3.0")
                url.set("https://www.gnu.org/licenses/gpl-3.0.en.html")
            }
        }
        developers {
            developer {
                id.set("skylarkarms")
                name.set("Andrew Andrade")
                email.set("juanandrade_20@hotmail.com")
            }
        }
        scm {
            String gitName = projectName.concat(".git")
            connection.set("scm:git:git://github.com/Skylarkarms/".concat(gitName))
            developerConnection.set("scm:git:ssh://git@github.com:Skylarkarms/".concat(gitName))
            url.set("https://github.com/Skylarkarms/".concat(projectName))
        }
    }
}


javadoc {

    // Fixes javadoc 16+ NPE bug by allowing custom javadoc path override.
    // If not set, uses default Gradle javadoc executable
    if (project.hasProperty('javadoc_17_path')) {
        executable = project.property('javadoc_17_path')
    }

    options.tags = [
            'implNote:a:Implementation Note:',
            'implSpec:a:Implementation Requirements:',
            'apiNote:a:API Note:',
            'param',
            'return',
            'throws'
    ]
}

tasks.withType(JavaCompile) {
    options.compilerArgs += "-Xlint:unchecked"
}

repositories {
    mavenCentral()
}

dependencies {
    implementation 'io.github.skylarkarms:lambdas:1.0.4'
    implementation 'io.github.skylarkarms:concur:1.1.1'
    testImplementation 'io.github.skylarkarms:lambdaprofiler:1.0.6'
    testImplementation 'io.github.skylarkarms:print:1.0.6'
}